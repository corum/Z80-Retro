Name     IO_DEC ;
PartNo   IO_DEC ;
Date     30/11/2022 ;
Revision 01 ;
Designer Peter Wilson ;
Company  Home ;
Assembly None ;
Location  ;
Device   G16V8A ;

/*
 *  I/O and signal decoder for I/O PIO card.
 */

/** input pins **/
PIN 2	 = GST;            /* Generic I/O range select from the CPU board */
PIN 3  = A0;             /* Address lines */
PIN 4  = A1;
PIN 5  = A2;
PIN 6  = COL0;           /* The COL0 input from the keyboard */
PIN 7  = ARDY;           /* The ARDY line from the sound chip */
PIN 8  = SNDSEL;         /* Bit 7 from the keyboard board output control register */
PIN 9  = RD;             /* Z80 RD */
PIN 10 = WR;             /* Z80 WR */

/** output pins **/
PIN 19  = PIOSEL;        /* Low to select PIO */
PIN 18  = RD_IN;         /* Read strobe (active low) for input latch */
PIN 17  = WR_OUT;        /* Write strobe (active high) to output 8 bit port register */
PIN 16  = L0;            /* Either COL0 if SNDSEL=0 or ARDY if SNDSEL=1 */
PIN 15  = ACE;           /* Chip select for the sound chip */
PIN 14  = ALWR;          /* Strobe data to the 8 bit audio data latch */

PIOSEL  = !GST & !A2;    /* PIO is selected for all 0XXb addresses */
RD_IN   = !(!GST & !RD & !A0 & !A1 & A2);
                         /* Active LOW read of the keyboard input register on address 100b */
WR_OUT  = !GST & !WR & !A0 & !A1 & A2;
                         /* Active HIGH write to keyboard port latch  on port 100b*/
L0      = (!SNDSEL & COL0) | (SNDSEL & ARDY);
                         /* Select either COL0 or ARDY depending on SNDSEL */
ACE     = !GST & A0 & !A1 & A2;
                         /* Sound chip selected at address 101b */
ALWR    = !GST & A0 & !A1 & A2 & !WR;
                         /* Active high write to the sound chip latch register on 101b */
